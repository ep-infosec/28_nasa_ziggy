// Download and build HDF5.
//
// TODO Next time around, parameterize the version and possibly the URL

task buildHdf5() {
    def tmp = file("$buildDir/tmp/hdf5")
    def hdf5 = file("$tmp/hdf5-1.12.2")
    def lib = file("$buildDir/lib")

    outputs.file "$lib/jarhdf5-1.12.2.jar"

    doLast() {
        tmp.mkdirs()

        // Use the SHA256 links at https://www.hdfgroup.org/downloads/hdf5/source-code/ to determine the URL. 
        exec {
            workingDir tmp
            commandLine "curl", "-o", "hdf5-1.12.2.#1", "https://hdf-wordpress-1.s3.amazonaws.com/wp-content/uploads/manual/HDF5/HDF5_1_12_2/source/hdf5-1.12.2.{tar.bz2,sha256}"
        }
        exec {
            workingDir tmp
            commandLine "tar", "-xf", "hdf5-1.12.2.tar.bz2"
        }
        exec {
            workingDir hdf5
            commandLine "sh", "-c", "./configure --with-zlib=/usr --prefix=$buildDir --enable-threadsafe --with-pthread=/usr --enable-unsupported --enable-java"
        }
        exec {
            workingDir hdf5
            commandLine "make"
        }
        exec {
            workingDir hdf5
            commandLine "make", "install"
        }
        copy {
            from("$tmp/lib") {
                include "*.jar"
            }
            into "$lib"
        }
    }
}

assemble.dependsOn buildHdf5
